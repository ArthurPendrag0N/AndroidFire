apply plugin: 'com.android.application'
def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId "jaydenxiao.com.androidfire"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 4
        versionName "1.0.3"

        multiDexEnabled true
        resValue "string", "updatefun_provider_file_authorities", "jaydenxiao.com.androidfire.fileprovider"
    }
    buildTypes {
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //apk命名
            android.applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        //这里修改apk文件名
                        def fileName = "AndroidFire-${defaultConfig.versionCode}-${defaultConfig.versionName}-${releaseTime()}.apk"
                        //output.outputFile = new File(outputFile.parent, fileName)
                        outputFileName = fileName
                    }
                }
            }
        }
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
        }
    }

    //打包不检查
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    viewBinding {
        enabled = true
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    //图片选择
    api project(path: ':library:photoPicker')//图片选择
    api project(path: ':library:irecyclerview')//万能适配器和recyclerview
    api project(path: ':library:oneKeyShareSDK')//分享
    api project(path: ':library:jcvideoplayer')//视频播放
    api project(path: ':library:selectordialog')//常用提示框
}
